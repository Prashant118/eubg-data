# Own prefixes
prefix ebg:                 <http://data.businessgraph.eu/ontology#>
prefix nuts:                <http://nuts.geovocab.org/id/> 
prefix ramon:               <http://rdfdata.eionet.europa.eu/ramon/ontology/> 
prefix rdf:                 <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs:                <http://www.w3.org/2000/01/rdf-schema#> 
prefix xs:                  <http://www.w3.org/2001/XMLSchema#> 
prefix spatial:             <http://geovocab.org/spatial#> 
prefix lau:                 <http://data.businessgraph.io/lau/>
prefix dbo:                 <http://dbpedia.org/ontology/>
prefix void:                <http://rdfs.org/ns/void#>

construct {
    ?lau2 a ebg:LAURegion, spatial:Feature;
          ramon:name            ?latin_name;
          rdfs:label            ?label;
          void:inDataset        lau:; 
          ramon:name            ?native_name;
          ramon:name            ?lbl_de, ?lbl_it, ?lbl_fr;
          dbo:populationTotal   ?population;
          dbo:areaTotal         ?area_size;
          ramon:code            ?lau2_base ;
          spatial:PP            ?lau1;
          spatial:PP            ?nuts_lau2;
          ramon:level           5.
    ?lau1 a ebg:LAURegion, spatial:Feature;
          ramon:level           4;
          void:inDataset        lau:;
          spatial:PP            ?nuts_lau1.
} WHERE {
    # $language and $country are externall variables replaced by loop.sh
    BIND("http://data.businessgraph.io/lau/" as ?base)
    BIND(CONCAT(UCASE("{country}"), "-", ?LAU2_NAT_CODE) as ?lau2_base)
    BIND(URI(CONCAT(?base, ?lau2_base)) as ?lau2)

    BIND(IF(?LAU1_NAT_CODE = "n.a." || ?LAU1_NAT_CODE = "" || ?LAU1_NAT_CODE = "no" || 
            regex(?NUTS_3, "^IE"), 
            ?undef,
            URI(CONCAT(?base, UCASE("{country}"), "-", ?LAU1_NAT_CODE))) as ?lau1)

    BIND(IF(regex(?POP, "[^0-9]"),
            ?undef,
            xs:integer(?POP)) as ?population)

    BIND(IF(regex(?AREA, "[^0-9]"),
            ?undef,
            xs:integer(?AREA)) as ?area_size)

    BIND(IF(?NUTS_3 = "0" || ?NUTS_3 = "" || BOUND(?lau1), 
            ?undef,
            URI(CONCAT("http://nuts.geovocab.org/id/", ?NUTS_3))) as ?nuts_lau2)

    BIND(IF(?NUTS_3 = "0" || ?NUTS_3 = "", 
            ?undef,
            URI(CONCAT("http://nuts.geovocab.org/id/", ?NUTS_3))) as ?nuts_lau1)

    BIND(IF(?NAME_2_LAT = "",
            ?undef,
            strlang(IF(regex(?NAME_2_LAT, "^\\(.*\\)$"),
                       concat(replace(?NAME_2_LAT, "^\\((.*)\\)$", "$1"), ?NAME_1),
                       ?NAME_2_LAT), "en")) as ?latin_name)

    BIND(IF(?NAME_1 = "",
            ?undef,
            IF("{language}" = "NA", ?NAME_1,
            strlang(?NAME_1, "{language}"))) as ?native_name)

    BIND(concat(?lau2_base, " ", IF(BOUND(?latin_name), ?latin_name, ?NAME_1)) as ?label)
    BIND(STRLANG(?NAME_DE, "de") as ?lbl_de)
    BIND(STRLANG(?NAME_IT, "it") as ?lbl_it)
    BIND(STRLANG(?NAME_FR, "fr") as ?lbl_fr)
}

